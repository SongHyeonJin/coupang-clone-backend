plugins {
	id 'org.springframework.boot' version '3.2.5' apply false
	id 'io.spring.dependency-management' version '1.1.7'apply false
	id "org.sonarqube" version "4.4.1.3373"
	id 'java'
}

sonarqube {
	properties {
		property "sonar.projectKey", "coupang-clone"
		property "sonar.host.url", "http://localhost:9000"
		property "sonar.login", System.getenv("SONAR_TOKEN")
	}
}

allprojects {
	group = 'com.example'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'io.spring.dependency-management'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	sourceSets {
		main {
			resources {
				srcDirs 'src/main/resources', 'src/main/resources/yaml'
			}
		}
	}

	tasks.named('processResources', Copy) {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
		from('src/main/resources/yaml') {
			include '**/*.yml'
			into ''
		}
	}
}
